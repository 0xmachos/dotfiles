#!/usr/bin/env zsh
# dotfiles/bittersweet

# bittersweet
#   Configures macOS just the way I like it

set -euo pipefail
# -e exit if any command returns non-zero status code
# -u prevent using undefined variables
# -o pipefail force pipelines to fail on first non-zero status code

IFS=$'\n\t'
# Set Internal Field Separator to newlines and tabs
# This makes bash consider newlines and tabs as separating words
# See: http://redsymbol.net/articles/unofficial-bash-strict-mode/

readonly SCRIPT_DIR="${0:A:h}"
# Absolute path to the directory containing this script
# $0  - path used to invoke the script (e.g. /usr/local/bin/bittersweet)
# :A  - resolve symlinks to the real absolute path
# :h  - take the head (parent directory), stripping the filename
# This ensures paths resolve correctly regardless of $PWD

### Define Colours ###

/usr/bin/tput sgr0;
# reset colors
RESET=$(/usr/bin/tput sgr0);
readonly RESET
BOLD=$(/usr/bin/tput bold);
readonly BOLD
RED=$(/usr/bin/tput setaf 1);
readonly RED

### END Colours ###


function usage {

  echo -e "\\nConfigures macOS just the way I like it ü§ì\\n"
  echo "Usage:"
  echo "  defaults              - Write new system and application default settings"
  echo "  dotfiles              - Symlink various config files into ${HOME}"
  echo "  scripts               - Symlink scripts in bin into /usr/local/bin"
  echo "  sudotouch             - Enable TouchID to sudo"
  echo "  secretive             - Install Secretive üîë"
  echo "  sublimetext           - Install Sublime Text"
  echo "  tower                 - Install Tower"
  echo "  deckset               - Install Deckset"
  echo "  maccy                 - Install Maccy"
  echo "  texpad                - Install Texpad (Texifier) & BasicTeX"
  echo "  basictex              - Install MacTeX BasicTeX Distribution"
  echo "  latex                 - Install LaTeX packages via tlmgr"
  echo "  blockblock            - Install BlockBlock"
  echo "  oversight             - Install OverSight üì∏üé§"
  echo "  brew                  - Install Homebrew"
  echo "  brewfile              - Install Homebrew packages from Brewfile"
  echo "  compinit              - Fix permissions on zsh completion directory /usr/local/share"
  echo "  shell                 - Change shell to zsh as installed by Brew"
  echo "  mdmprofile            - Open System Preferences to install MDM Profile"

  echo -e "\\n  ${RED}hailmary${RESET}              - ${BOLD}Run every function${RESET} in order listed above"

  exit 0
}


### Utility Functions ###
# check_os_compatibility
# check_developer_tools
# download_application
# verify_sha
# verify_signature
# install_application
# objective_see_install
# get_sudo
# ctrl_c
# run_test


function check_os_compatibility {
  # check_os_compatibility
  #   Check if the host is darwin (i.e macOS)
  #   Check if the host is ruuning macOS $supported_macos_version

  local os
  local current_macos_version
  local -r supported_major_macos_version="26"
  local -r supported_macos_name="Tahoe"
  os="$(uname -s)"

  if [[ "${os}" != "Darwin" ]]; then 
    echo "[‚ùå] This script was written for macOS"
    exit 1
  fi
  
  current_macos_version="$(sw_vers -productVersion | awk -F '.' '{print $1}')";
  
  if [[ "${current_macos_version}" != "${supported_major_macos_version}" ]]; then
    echo "[‚ö†Ô∏è ] This scipt was written for macOS ${supported_macos_name} (${supported_major_macos_version})"
    sleep 3
  fi 
}


function check_developer_tools {
  # check_developer_tools
  #   Checks if xcode command line tools are installed

  if ! xcode-select -p >/dev/null 2>&1; then 
    echo "[‚ùå] Command line developer tools required"
    echo "[üç∫] Install via: xcode-select --install"
    exit 1
  fi
}


function download_application {
  # download_application
  #   download_application "download_path" "url"
  #   Download file using curl
  #   curl will follow redirects (--location)

  local download_path=${1:?download_path not passed to download_application}
  local url=${2:?url not passed to download_application}
  local file_name
  file_name=$(echo "${download_path}" | awk -F '/' '{print $(NF)}')

  echo "[üç∫] Downloading ${file_name}"
  
  if curl --location --silent "${url}" --output "${download_path}"; then 
  # Download 
    echo "[‚úÖ] Successfully downloaded ${file_name}"
  else
    echo "[‚ùå] Failed to download ${file_name}"
    return 1
  fi
}


function verify_sha {
  # verify_sha
  #   Uses shasum to check hash of file is what
  #   we expect it to be

  local file_name=${1:?file_name not passed to verify_sha}
  local file_download_path=${2:?file_download_path not passed to verify_sha}
  local file_sha_length=${3:?file_sha_length not passsed to verify_sha}
  local file_sha=${4:?file_sha not passed to verify_sha}


  echo "[üç∫] Attempting to validate ${file_name} SHA${file_sha_length} hash"

  echo "${file_sha}  ${file_name}" > "${file_download_path}.sha${file_sha_length}"

  (
    # Execute in a subshell so the working directory is not permanantly changed

    cd "${HOME}/Downloads" || exit

    if shasum -a "${file_sha_length}" -c "${file_download_path}.sha${file_sha_length}" >/dev/null; then 
    # Attempt to validate the ZIPs SHA256 hash
      echo "[‚úÖ] Successfully validated ${file_name} SHA${file_sha_length} hash"
    else
      echo "[‚ùå] Failed to validate ${file_name} SHA${file_sha_length} hash"
      exit 1
    fi

  )

  rm "${file_download_path:?}.sha${file_sha_length:?}"
}


function verify_signature {
  # verify_signature
  #   Check that .app and .pkg files are signed, if not hard exit 
  #   Check that .app and .pkg files are notarised, if not print warning
  # Paramters: 
  #   $file_path REQUIRED
  #     Path to .app or .pkg file
  #     e.g. $HOME/Downloads/Maccy.app
  #   $application_name REQUIRED
  #     Name of Application being installed
  #     e.g Maccy

  local file_path=${1:?file_path not passed to verify_signature}
  local application_name=${2:?application_name not passed to verify_signature}

  if [[ "${file_path}" =~ \.pkg$ ]]; then

    local package_name
    package_name="$(basename "${file_path}")"

    echo "[üç∫] Attempting to validated the signature on ${package_name}"
    
    if /usr/sbin/pkgutil --check-signature "${file_path}" >/dev/null; then 
      echo "[‚úÖ] Successfully validated the signaturee on ${package_name}"
    else
      echo "[‚ùå] Failed to validate the signature on ${package_name}"
      exit 1
    fi

    if /usr/sbin/spctl --assess --type install "${file_path}"; then
      echo "[‚úÖ] ${package_name} is notarised"
      return 0
    else
      echo "[‚ùå] ${package_name} is NOT notarised"
    fi
  fi


  if [[ "${file_path}" =~ \.app$ ]]; then

    echo "[üç∫] Attempting to validated the signature on ${application_name}.app"

    if /usr/bin/codesign --verify --deep --strict "${file_path}"; then
      # Check the .app is correctly signed
      echo "[‚úÖ] Successfully validated the signaturee on ${application_name}.app"
    else
      echo "[‚ùå] Failed to validate the signature on ${application_name}.app"
      exit 1
    fi

    if /usr/sbin/spctl --assess --type execute "${file_path}"; then
      echo "[‚úÖ] ${application_name}.app is notarised"
      return 0
    else
      echo "[‚ùå] ${application_name}.app is NOT notarised"
    fi

  else
    echo "[‚ùå] ${file_path} is not a .app or .pkg file"
    exit 1
  fi
}


function install_application {
  # install_application
  #   Install Applications packaged as .dmg or .zip
  #   Does not work for application which require an installer (.pkg)
  #   Checks if .dmg or .zip then act accordingly
  #   Checks signature on .app before installing

  local application_name=${1:?application_name not passed}
  local downloaded_file_path=${2:?downloaded_file_path not passed}
  local downloaded_file_name
  local dmg_mount_path
  downloaded_file_name="$(echo "${downloaded_file_path}" | awk -F '/' '{print $NF}')"


  if file "${downloaded_file_path}" | grep --quiet 'Zip'; then
    # IF file is a Zip 

    echo "[üç∫] Attempting to unzip ${downloaded_file_name}"
    if ditto -x -k "${downloaded_file_path}" "${HOME}/Downloads"; then
      # Attempt to unzip
      echo "[‚úÖ] Successfully unzipped ${downloaded_file_name}"
    else
      echo "[‚ùå] Failed to unzip ${downloaded_file_name}"
      exit 1
    fi

    verify_signature "${HOME}/Downloads/${application_name}.app" "${application_name}"

    echo "[üç∫] Attempting to copy ${application_name}.app into /Applications"
    if cp -Ri "${HOME}/Downloads/${application_name}.app" "/Applications"; then
      # Attempt to copy .app into /Applications 
      echo "[‚úÖ] Successfully installed ${application_name}"
    else
      echo "[‚ùå] Failed to install ${application_name}"
      exit 1
    fi

    ## Cleanup
    echo "[üç∫] Deleting ${downloaded_file_path}"
    # Delete the ZIP
    rm "${downloaded_file_path:?}"

    echo "[üç∫] Deleting ${HOME}/Downloads/${application_name}.app"
    # Delete the .app
    rm -r "${HOME:?}/Downloads/${application_name:?}.app"


  elif file "${downloaded_file_path}" | grep --quiet 'dmg'; then
    # IF file is a dmg
    
    echo "[üç∫] Attempting to mount ${downloaded_file_path}"
    dmg_mount_path="$(hdiutil attach -nobrowse "${downloaded_file_path}" | grep -o '/Volumes/.*')"
    # Parse the actual mount point from hdiutil output

    if [[ -z "${dmg_mount_path}" ]]; then
      echo "[‚ùå] Failed to mount ${downloaded_file_path}"
      exit 1
    fi

    echo "[‚úÖ] Successfully mounted ${downloaded_file_name} at ${dmg_mount_path}"

    verify_signature "${dmg_mount_path}/${application_name}.app" "${application_name}"

    echo "[üç∫] Attempting to copy ${application_name}.app into /Applications"
    if cp -Ri "${dmg_mount_path}/${application_name}.app" "/Applications"; then
      # Attempt to copy .app into /Applications 
      echo "[‚úÖ] Successfully installed ${application_name}"
    else
      echo "[‚ùå] Failed to install ${application_name}"
      exit 1
    fi

    ## Cleanup 
    echo "[üç∫] Unmounting ${dmg_mount_path}"
    # Unmount the DMG
    hdiutil detach -quiet "${dmg_mount_path}"
    
    echo "[üç∫] Deleting ${downloaded_file_path}"
    # Delete the DMG
    rm "${downloaded_file_path:?}"

  fi  
}


function objective_see_install {
  # objective_see_install
  #   Installs tools from objective-see.com
  #   Currently used for BlockBlock & Oversight

  local application_name=${1:?application_name not passed to objective_see_install}
  local downloaded_file_path=${2:?downloaded_file_path not passed objective_see_install}
  local downloaded_file_name
  local installer_file_name
  downloaded_file_name=$(echo "${downloaded_file_path}" | awk -F '/' '{print $(NF)}')
  installer_file_name="${application_name} Installer"


  echo "[üç∫] Attempting to unzip ${downloaded_file_path}"
  
  if unzip -qa "${downloaded_file_path}" -d "${HOME}/Downloads"; then
    # Attempt to unzip
    echo "[‚úÖ] Successfully unzipped ${downloaded_file_name}"
  else
    echo "[‚ùå] Failed to unzip ${downloaded_file_name}"
    exit 1
  fi

  verify_signature "${HOME}/Downloads/${installer_file_name}.app" "${application_name}"

  get_sudo "to install ${application_name}"

  if sudo "${HOME}/Downloads/${installer_file_name}.app/Contents/MacOS/${installer_file_name}" -install >/dev/null; then
    echo "[‚úÖ] Successfully installed ${application_name}"
  else
    echo "[‚ùå] Failed to install ${application_name}"
    exit 1
  fi

  ## Cleanup

  echo "[üç∫] Deleting ${downloaded_file_path}"
  # Delete the zip
  rm "${downloaded_file_path:?}"

  echo "[üç∫] Deleting ${HOME}/Downloads/${installer_file_name}.app"
  # Delete installer application bundle
  rm -rf "${HOME:?}/Downloads/${installer_file_name:?}.app"

}


function get_sudo {

  # Ask user to input password to get sudo privilege
  # Update user's cached credentials, adds five (5)
  #   minutes to sudo timeout (-v)
  # Paramter: 
  #  $purpose_string REQUIRED
  #   Describe why sudo is needed 

  local purpose_string=${1:?purpose_string not passed to get_sudo}
  
  if sudo --prompt="[‚ö†Ô∏è ] Password required ${purpose_string}: " -v; then
    # !! SUDO !!
    return 0
  else 
    return 1
  fi
}


function ctrl_c {
  # ctrl_c
  #   If user hits CTRL + C exit 
  echo -e "\\n[‚ùå] ${USER} has chosen to quit!"
  exit 1
}


function run_test {

  "${SCRIPT_DIR}/test"
}




################################
################################
#### END Utility Functions #####
################################
################################




function write_defaults {

  echo "[üç∫] Writing system & application defaults"

  defaults write com.apple.TextEdit RichText -bool false
  # TextEdit: Use Plain Text Mode as Default
  # Default: com.apple.TextEdit RichText -bool true

  defaults write com.apple.TextEdit TabWidth -int 4
  # TextEdit: Set tab width to 4 spaces
  # Default: n/a

  defaults write -g NSNavPanelExpandedStateForSaveMode -bool true
  defaults write -g NSNavPanelExpandedStateForSaveMode2 -bool true
  # Save Panel: Show expanded version 
  # Default: defaults write -g NSNavPanelExpandedStateForSaveMode -bool false && \
  # defaults write -g NSNavPanelExpandedStateForSaveMode2 -bool false

  defaults write com.apple.finder ShowStatusBar -bool true
  # Finder: Show the status bar
  # Default: defaults write com.apple.finder ShowStatusBar -bool false

  defaults write com.apple.AppleMultitouchTrackpad TrackpadThreeFingerDrag -bool true
  # Trackpad: Enable three finger drag
  # Default: defaults write com.apple.AppleMultitouchTrackpad TrackpadThreeFingerDrag -bool false

  defaults write com.apple.AppleMultitouchTrackpad Clicking -bool true
  # Trackpad: Enable tap to click
  # Default: defaults write com.apple.AppleMultitouchTrackpad Clicking -bool false

  defaults write com.apple.menuextra.clock DateFormat -string "EEE d MMM  HH:mm"
  # Clock: Set format to Sun 26 Nov 16:00 

  defaults write com.apple.mail PreferPlainText -bool true
  # Mail: Force emails to be opened in plain text
  # Default: defaults write com.apple.mail PreferPlainText -bool false

  defaults write com.apple.ActivityMonitor IconType -int 5
  # Activity Monitor: Set dock icon to "Show CPU Usage"
  # Default: n/a

  defaults write com.apple.finder FXRemoveOldTrashItems -bool true
  # Finder: Empty trash after 30 days
  # Default: defaults write com.apple.finder FXRemoveOldTrashItems -bool false

  defaults write com.apple.desktopservices DSDontWriteNetworkStores -bool true
  # SMB: Don't write .DS_Store files on SMB shares
  # Default: defaults write com.apple.desktopservices DSDontWriteNetworkStores -bool false
}


function install_dotfiles {
  # install_dotfiles
  #   Create directories in $HOME
  #   Soft links config files from repo into $HOME
  #   Config files for: zsh, git, ssh

  local -ar dot_dirs=(.ssh .extra .config/git .claude)
  local dot_dir
  local -ar dot_files=(.zshrc .functions .aliases)
  local dot_file
  local -ar ssh_configs=(config .extra_ssh)
  local ssh_config
  local -ar git_configs=(config ignore allowed_signers)
  local git_config

  # Create required directories
  for dot_dir in "${dot_dirs[@]}"; do
    if mkdir -p "${HOME}/${dot_dir}"; then
      echo "[‚úÖ] Successfully created ${HOME}/${dot_dir}"
    else
      echo "[‚ùå] Failed to create ${HOME}/${dot_dir}"
    fi
  done

  # Symlink general dotfiles into $HOME
  for dot_file in "${dot_files[@]}"; do
      if ln -sfn "${SCRIPT_DIR}/${dot_file}" "${HOME}/${dot_file}"; then
        echo "[‚úÖ] Successfully linked ${SCRIPT_DIR}/${dot_file} to ${HOME}/${dot_file}"
      else
        echo "[‚ùå] Failed to link ${SCRIPT_DIR}/${dot_file} to ${HOME}/${dot_file}"
      fi
  done

  # Symlink ssh config in to $HOME/.ssh/
  for ssh_config in "${ssh_configs[@]}"; do
    if ln -sfn "${SCRIPT_DIR}/.ssh/${ssh_config}" "${HOME}/.ssh/${ssh_config}"; then
      echo "[‚úÖ] Successfully linked ${ssh_config} to ${HOME}/.ssh/${ssh_config}"
    else
      echo "[‚ùå] Failed to link ${ssh_config} to ${HOME}/.ssh/${ssh_config}"
    fi
  done

  # Symlink git config files into $HOME/.config/git/
  for git_config in "${git_configs[@]}"; do
    if ln -sfn "${SCRIPT_DIR}/.config/git/${git_config}" "${HOME}/.config/git/${git_config}"; then
      echo "[‚úÖ] Successfully linked ${git_config} to ${HOME}/.config/git/${git_config}"
    else
      echo "[‚ùå] Failed to link ${git_config} to ${HOME}/.config/git/${git_config}"
    fi
  done

  # Symlink Claude Code settings into $HOME/.claude/
  if ln -sfn "${SCRIPT_DIR}/settings.json" "${HOME}/.claude/settings.json"; then
    echo "[‚úÖ] Successfully linked settings.json to ${HOME}/.claude/settings.json"
  else
    echo "[‚ùå] Failed to link settings.json to ${HOME}/.claude/settings.json"
  fi

  # Harden file permissions
  # git clone sets 644 by default; enforce 600 on sensitive files
  # chmod follows symlinks so this works on both the source and linked paths
  local -ar sensitive_files=(
    "${SCRIPT_DIR}/.ssh/config"
    "${SCRIPT_DIR}/.ssh/.extra_ssh"
    "${SCRIPT_DIR}/.config/git/config"
    "${SCRIPT_DIR}/.config/git/ignore"
    "${SCRIPT_DIR}/.config/git/allowed_signers"
    "${SCRIPT_DIR}/.zshrc"
    "${SCRIPT_DIR}/.aliases"
    "${SCRIPT_DIR}/settings.json"
  )
  local sensitive_file

  for sensitive_file in "${sensitive_files[@]}"; do
    if chmod 600 "${sensitive_file}"; then
      echo "[‚úÖ] Hardened permissions on ${sensitive_file##*/}"
    else
      echo "[‚ùå] Failed to harden permissions on ${sensitive_file##*/}"
    fi
  done
}


function install_scripts {
  # install_scripts
  #   Symlink dotfiles/bin/ scripts into /usr/local/bin
  #   Create /usr/local/bin
  #   Symlink files in /usr/local/bin
  
  local -ar bin_files=(dns pihole_stats sublime_backup)
  local -ar macos_script_bin_files=(c0design)
  
  # Create /usr/local/bin
  if ! [[ -d "/usr/local/bin/" ]]; then 
    get_sudo "to create /usr/local/bin/"
    if sudo mkdir -p "/usr/local/bin/"; then
      echo "[‚úÖ] Successfully created /usr/local/bin/"
    else
      echo "[‚ùå] Failed to create /usr/local/bin/"
    fi
  fi

  get_sudo "to symlink scripts into /usr/local/bin/"
  
  # Symlink files
  for bin_file in "${bin_files[@]}"; do
    if sudo ln -sfn "${SCRIPT_DIR}/bin/${bin_file}" "/usr/local/bin/${bin_file}"; then
      echo "[‚úÖ] Successfully linked ${bin_file} to /usr/local/bin/${bin_file}"
    else
      echo "[‚ùå] Failed to link ${bin_file} to /usr/local/bin/${bin_file}"
    fi
  done

  # Symlink bittersweet into /usr/local/bin
  if sudo ln -sfn "${SCRIPT_DIR}/bittersweet" "/usr/local/bin"; then
    echo "[‚úÖ] Successfully linked bittersweet to /usr/local/bin/bittersweet"
  else
    echo "[‚ùå] Failed to link bittersweet to /usr/local/bin/bittersweet"
  fi

  # Symlink some files in macos-scripts repo into /usr/local/bin
  for macos_script in "${macos_script_bin_files[@]}"; do
    if sudo ln -sfn "$HOME/Documents/Projects/macos-scripts/${macos_script}" "/usr/local/bin/${macos_script}"; then
      echo "[‚úÖ] Successfully linked ${macos_script} to /usr/local/bin/${macos_script}"
    else
      echo "[‚ùå] Failed to link ${macos_script} to /usr/local/bin/${macos_script}"
    fi
  done
}


function enable_touchid_sudo {
  # enable_touchid_sudo
  #   As of macOS Sonoma (14.0) this setting can persist OS updates when set in  
  #     /etc/pam.d/sudo_local. See: https://support.apple.com/en-us/HT213893
  #   Check if already enabled in /etc/pam.d/sudo and warn user
  #   Check if /etc/pam.d/sudo_local exists, if not create it
  #   Use vim to insert required text to enable TouchID for sudo into sudo_local
  
  if grep -q 'pam_tid.so' "/etc/pam.d/sudo"; then
    logger -p user.warning -s "Enabling TouchID for sudo in /etc/pam.d/sudo does not persist across updates.\
                              See https://support.apple.com/en-us/HT213893"
  fi

  if [[ -e "/etc/pam.d/sudo_local" ]]; then
    if grep -q 'pam_tid.so' "/etc/pam.d/sudo_local"; then
      logger -p user.info -s "TouchID for sudo already enabled"
      return 0
    fi
  
  else

    get_sudo "to create /etc/pam.d/sudo_local"
    
    if sudo install -m "444" -g "wheel" -o "root" "/dev/null" "/etc/pam.d/sudo_local"; then
      # Use install to create the file /etc/pam.d/sudo_local
      # Copies from /dev/null which creates an empty file
      # Set permissions to read only, group to wheel and owner to root 
      logger -p user.info -s "Created /etc/pam.d/sudo_local"
    else
      logger -p user.error -s "Failed to create /etc/pam.d/sudo_local"
      return 1
    fi

    if sudo ex -s -c '1i|auth       sufficient     pam_tid.so' -c x! -c x! "/etc/pam.d/sudo_local"; then
      # Invoke Vim in ex mode
      # Select line 1, enter insert mode, insert that text write changes and exit
      # Need to exit twice to get passed the read only file warning

      logger -p user.info -s "TouchID sudo Enabled"
      return 0
    else
      logger -p user.error -s "Failed to enable TouchID sudo"
      return 1
  fi
fi
}


function install_secretive {
  # install_secretive
  #   Installs Secretive via GitHub releases API + plutil
  #   https://github.com/maxgoedjen/secretive/issues/117

  if [[ -d "/Applications/Secretive.app" ]]; then
    echo "[üç∫] Secretive already installed"
    return 0
  fi

  echo "[üç∫] Installing Secretive"

  local api_response
  local zip_download_url
  local zip_download_path

  api_response="$(curl -s "https://api.github.com/repos/maxgoedjen/secretive/releases/latest")"
  zip_download_url="$(echo "${api_response}" | plutil -extract "assets.0.browser_download_url" raw -o - -- -)"

  if [[ -z "${zip_download_url}" ]]; then
    echo "[‚ùå] Failed to get Secretive download URL from GitHub API"
    return 1
  fi

  zip_download_path="${HOME}/Downloads/$(basename "${zip_download_url}")"

  download_application "${zip_download_path}" "${zip_download_url}"

  install_application "Secretive" "${zip_download_path}"
}


function install_sublime_text {

  if [[ -d "/Applications/Sublime Text.app" ]]; then
    echo "[üç∫] Sublime Text already installed"
    return 0
  fi

  echo "[üç∫] Installing Sublime Text"

  local latest_build  
  latest_build="$(curl -s https://www.sublimetext.com/download \
                  | awk -F 'Build ' 'BEGIN {found=0} /Build/ && !found {sub("</p>", "", $2); print $2; found=1}')"
  # Get the latest build number
  # TIL https://stackoverflow.com/a/28879552

  local zip_name="sublime_text_build_${latest_build}_mac.zip"
  local zip_download_path="${HOME}/Downloads/${zip_name}"

  download_application "${zip_download_path}" "https://download.sublimetext.com/sublime_text_build_${latest_build}_mac.zip"

  install_application "Sublime Text" "${zip_download_path}"

  ## Install config files
  echo "[üç∫] Installing Sublime Text config files"

  if mkdir -p "$HOME/Library/Application Support/Sublime Text 3/Packages/"; then
    echo "[‚úÖ] Successfully created ~/Library/Application Support/Sublime Text 3/Packages/"
  else
    echo "[‚ùå] Failed to create ~/Library/Application Support/Sublime Text 3/Packages/"
    exit 1
  fi 

  if cp -r "${SCRIPT_DIR}/Sublime Text 3/Packages/User" "$HOME/Library/Application Support/Sublime Text 3/Packages"; then
    echo "[‚úÖ] Successfully installed Sublime Text config files"
  else
    echo "[‚ùå] Failed to install Sublime Text config files"
    exit 1
  fi
}


function install_tower {

  if [[ -d "/Applications/Tower.app" ]]; then
    echo "[üç∫] Tower already installed"
    return 0
  fi

  echo "[üç∫] Installing Tower"

  local url
  local zip_name
  local zip_download_path

  url="$(curl -sI https://updates.fournova.com/tower3-mac/stable/releases/latest/download \
        | grep 'Location' \
        | awk '{print $2}' \
        | tr -d '\r')"
  zip_name="$(echo "${url}" | awk -F '/' '{print $7}')"
  zip_download_path="${HOME}/Downloads/${zip_name}" 

  download_application "${zip_download_path}" "${url}"

  install_application "Tower" "${zip_download_path}"
}


function  install_deckset {
  # install_deckset
  #   Install deckset presentation software
  #   $url prebuilt, getting $version and $build from the http location header. 
  #   Grabbing $url directly from curl output does not work.
  #     curl -sLI https://www.decksetapp.com/download | grep 'location: ' | awk 'END{print}'
  #     Produces: 'location: https://dl.decksetapp.com/Deckset+2.0.16+(2578).dmg\C-M'
  #     The "\C-M" string at the end is erroneous. It does not appear when run from
  #     the command line, only when run in the script with set -x. 
  #     It's apprently an ascci carriage return. 
  #     Failed to remove with: tr -d '\C-M', tr -d '\n', tr -d '\r' 

  if [[ -d "/Applications/Deckset.app" ]]; then
    echo "[üç∫] Deckset already installed"
    return 0
  fi
  
  echo "[üç∫] Installing Deckset"
  
  local location_header
  local version
  local build
  local url
  local dmg_name
  local dmg_download_path
  
  location_header="$(curl -sLI https://www.decksetapp.com/download | grep 'dl.decksetapp')"
  version="$(echo -e "${location_header}" | awk -F '[+|+]' '{print $2}')"
  build="$(echo -e "${location_header}" | awk -F '[(|)]' '{print $2}')"
  url="https://dl.decksetapp.com/Deckset+${version}+(${build}).dmg"
  dmg_name=$(basename "${url}")
  dmg_download_path="${HOME}/Downloads/${dmg_name}"

  download_application "${dmg_download_path}" "${url}"

  install_application "Deckset" "${dmg_download_path}"
}


function install_maccy {
  # install_maccy
  #   Installs Maccy via GitHub releases API + plutil

  if [[ -d "/Applications/Maccy.app" ]]; then
    echo "[üç∫] Maccy already installed"
    return 0
  fi

  echo "[üç∫] Installing Maccy"

  local api_response
  local zip_download_url
  local zip_download_path

  api_response="$(curl -s "https://api.github.com/repos/p0deje/Maccy/releases/latest")"
  zip_download_url="$(echo "${api_response}" | plutil -extract "assets.0.browser_download_url" raw -o - -- -)"

  if [[ -z "${zip_download_url}" ]]; then
    echo "[‚ùå] Failed to get Maccy download URL from GitHub API"
    return 1
  fi

  zip_download_path="${HOME}/Downloads/$(basename "${zip_download_url}")"

  download_application "${zip_download_path}" "${zip_download_url}"

  install_application "Maccy" "${zip_download_path}"
}


function install_texpad {
  # install_texpad
  #   Installs Texpad via usual helpers

if [[ -d "/Applications/Texifier.app" ]]; then
  echo "[üç∫] Texifier already installed"
  return 0
fi

local dmg_download_url
local dmg_name
local dmg_download_path


dmg_download_url="$(curl -s https://www.texifier.com/mac \
                  | awk -F'href="' '{split($2, a, "\""); if(a[1] ~ /download\.texifier\.com/) print a[1]}')"
dmg_name="$(basename "${dmg_download_url}")"
dmg_download_path="${HOME}/Downloads/${dmg_name}"

download_application "${dmg_download_path}" "${dmg_download_url}"

install_application "Texifier" "${dmg_download_path}"
}

function install_basictex {
  # install_basictex
  #   Installs the MacTeX BasicTeX distribution via usual helpers

  if [[ -x "$(command -v tlmgr)" ]]; then
    echo "[üç∫] MacTeX already installed"
    return 0
  fi

  local pkg_download_url
  local pkg_name
  local pkg_download_path

  pkg_download_url="https://mirror.ctan.org/systems/mac/mactex/BasicTeX.pkg"
  pkg_name="$(basename "${pkg_download_url}")"
  pkg_download_path="$HOME/Downloads/${pkg_name}"

  download_application "${pkg_download_path}" "${pkg_download_url}"

  echo "[üç∫] Attempting to install ${pkg_name}"

  get_sudo "to install BasicTeX"

  if sudo installer -pkg "${pkg_download_path}" -target "/" >/dev/null; then
    # !! SUDO !!
    # Install
    echo "[‚úÖ] Successfully installed BasicTeX"
  else
    echo "[‚ùå] Failed to install BasicTeX"
    exit 1
  fi

  # Cleanup
  echo "[üç∫] Deleting ${pkg_download_path}"
  rm "${pkg_download_path:?}"
  # Delete the pkg
}


function install_latex_packages {
  # install_latex_packages
  #   Installs some LaTeX packages via tlmrg
  #   Updates tlmgr and all currently installed packages first

  echo "[üç∫] Updating tlmgr, all installed packages and installing packages..."
  sleep 2

  get_sudo "to update tlmgr, update installed packages and install packages"
  
  # Update tlmgr 
  if sudo tlmgr update --self >/dev/null; then
    echo "[‚úÖ] Successfully updated tlmgr"
  else
    echo "[‚ùå] Failed to update tlmgr"
  fi

  # Update packages installed via tlmgr 
  if sudo tlmgr update --all >/dev/null; then
    echo "[‚úÖ] Successfully updated LaTeX packages via tlmgr"
  else
    echo "[‚ùå] Failed to update LaTeX packages via tlmgr"
  fi

  local -ar PACKAGES=(sectsty helvetic)
  # sectsty: Required for the Custom sectioning
  # helvetic: Installs the helvetica font (phv) for the custom sectioning

  for PACKAGE in "${PACKAGES[@]}"; do
    if sudo tlmgr install "${PACKAGE}"; then
      echo "[‚úÖ] Successfully installed LaTeX package ${PACKAGE} via tlmgr"
    else
      echo "[‚ùå] Failed to install LaTeX package ${PACKAGE} via tlmgr"
    fi
  done
}


function install_blockblock {
  # install_blockblock
  #   Installs BlockBlock (BB) via GitHub releases API + plutil
  #   Uses objective_see_install for the actual installation

  if [[ -d "/Applications/BlockBlock Helper.app" ]]; then
    echo "[üç∫] BlockBlock already installed"
    return 0
  fi

  echo "[üç∫] Installing BlockBlock"

  local api_response
  local url
  local zip_name
  local zip_download_path

  api_response="$(curl -s "https://api.github.com/repos/objective-see/BlockBlock/releases/latest")"
  url="$(echo "${api_response}" | plutil -extract "assets.0.browser_download_url" raw -o - -- -)"

  if [[ -z "${url}" ]]; then
    echo "[‚ùå] Failed to get BlockBlock download URL from GitHub API"
    return 1
  fi

  zip_name="$(basename "${url}")"
  zip_download_path="${HOME}/Downloads/${zip_name}"

  download_application "${zip_download_path}" "${url}"

  objective_see_install "BlockBlock" "${zip_download_path}"

  echo "[‚ö†Ô∏è] Remember to enable Full Disk Access for BlockBlock"

  # Enable headless mode
  defaults write com.objectiveSee.BlockBlock headlessMode -bool true
}


function install_oversight {
  # install_oversight
  #   Installs OverSight via GitHub releases API + plutil
  #   Uses objective_see_install for the actual installation

  if [[ -d "/Applications/OverSight.app" ]]; then
    echo "[üç∫] OverSight already installed"
    return 0
  fi

  echo "[üç∫] Installing OverSight üì∏üé§"

  local api_response
  local url
  local zip_name
  local zip_download_path

  api_response="$(curl -s "https://api.github.com/repos/objective-see/OverSight/releases/latest")"
  url="$(echo "${api_response}" | plutil -extract "assets.0.browser_download_url" raw -o - -- -)"

  if [[ -z "${url}" ]]; then
    echo "[‚ùå] Failed to get OverSight download URL from GitHub API"
    return 1
  fi

  zip_name="$(basename "${url}")"
  zip_download_path="${HOME}/Downloads/${zip_name}"

  download_application "${zip_download_path}" "${url}"

  objective_see_install "OverSight" "${zip_download_path}"

  # Enable headless mode, auto updates & start on login
  defaults write com.objective-see.OverSight runHeadless   -bool true
  defaults write com.objective-see.OverSight check4Updates -bool true
  defaults write com.objective-see.OverSight startAtLogin  -bool true
} 


function install_brew {

  # Install Homebrew

  if ! [[ -x "$(command -v brew)" ]]; then

    echo "[üç∫] Installing Homebrew"
    echo -e "[‚ö†Ô∏è ] \\033[0;31mStick around\\033[0m - Requires you to press RETURN and input your password"
    sleep 5

    if /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install.sh)"; then
      echo "[‚úÖ] Successfully installed Homebrew üçª"
    else
      echo "[‚ùå] Failed to install Homebrew üò¢"
      exit 1
    fi    

  else

    echo "[üç∫] Updating Homebrew"
    sleep 2
    
    brew update
    brew upgrade
    brew cleanup
  fi
}


function install_brewfile {

  # Install Homebrew packages from dotfiles/.extra/Brewfile
  # Will install programs from the Mac AppStore if user is logged in

 if ! [[ -x "$(command -v mas)" ]]; then

    echo "[üç∫] Installing mas"

    if brew install mas 2>/dev/null; then
      echo "[‚úÖ] Installed mas"
    else
      echo "[‚ùå] Failed to install mas"
      return 1
    fi

  else
    echo "[üç∫] mas already installed"
  fi 


  echo "[üç∫] Installing Homebrew packages from Brewfile"
  get_sudo "to install software via Homebrew"

  if brew bundle install --file="${SCRIPT_DIR}/.extra/Brewfile"; then
    echo "[‚úÖ] Successfully installed packages from Brewfile"
    return 0
  else
    echo "[‚ùå] Failed to install packages from Brewfile"
    return 1
  fi
}


function zsh_compinit_fix {
  # zsh_compinit_fix
  #   Secure permissions on zsh completion files at /opt/homebrew/share
  #   https://stackoverflow.com/a/43544733
  #   https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
  #   Remove group/other write/execite permissions
  #   Change ownership to $USER:staff

  local -r completions_dir="$(brew --prefix)/share"
  
  echo "[üç∫] Fixing zsh completions permissions"

  if ! chmod -R go-wx "${completions_dir}"; then
    echo "[‚ùå] Failed to remove r/x for group & others on /usr/local/share/zsh"
    return 1
  fi
  
  if ! chown -R "${USER}:staff" "${completions_dir}"; then
    echo "[‚ùå] Failed to change owners of ${completions_dir} to mikey:staff"
    return 1
  fi

  echo "[‚úÖ] Fixed zsh completions permissions"
}


function change_shell {
  # change_shell
  #   Change default shell to zsh as installed by Brew
  #   /usr/local/bin/zsh instead of /bin/zsh
  # This has been removed from Hailmary as zsh does not
  #   get updated that much. It is safer to use the system 
  #   version which is protected by SIP & SSV

  local -r new_shell="/opt/homebrew/bin/zsh"
  
  if [[ "${SHELL}" == "${new_shell}" ]]; then
    echo "[‚úÖ] Current shell is already '${new_shell}'"
    return 0
  fi

  get_sudo "to change shell"

  if [[ ! -e "${new_shell}" ]]; then
    echo "[‚ùå] Failed to change shell as '${new_shell}' does not exist"
    return 1
  fi

  if ! grep -qx "${new_shell}" /etc/shells; then
    if ! sudo bash -c "echo ${new_shell} >> /etc/shells"; then
      # !! SUDO !!
      # Add new shell to list of allowed shells
      # https://unix.stackexchange.com/a/4833/271903
      echo "[‚ùå] Failed to add ${new_shell} to /etc/shells"
      return 1
    fi
  fi
  
  if chsh -s "${new_shell}"; then
  # Change $USER shell
    echo "[‚úÖ] Successfully change default shell to ${new_shell}"
    return 0
  else  
    echo "[‚ùå] Failed to change default shell to ${new_shell}"
    return 1
  fi
}


function open_system_preferences_mdm_profile {
  # open_system_preferences_mdm_profile
  # As of macOS Big Sur (11) /usr/bin/profiles can no longer install .mobileconfig profiles
  # MDM Profiles must be installed via the GUI via System Preferences > Profiles
  # However we can semi automate this process
  #   See: https://www.alansiu.net/2021/01/06/semi-automating-profile-installation-in-big-sur/

  for profile in "${SCRIPT_DIR}"/profiles/*.mobileconfig; do
    if ! open "/System/Library/PreferencePanes/Profiles.prefPane" "${profile:?}"; then
      echo "[‚ùå] Failed to open System Preferences > Profiles with ${profile:?}"
      return 1
    fi

    profile_name=$(echo "${profile}" | awk -F '/' '{print $2}')
    echo "[‚ö†Ô∏è ] Go to System Preferences > Profiles and install ${profile_name}"
  done

}


function main {

  check_os_compatibility
  check_developer_tools

  local cmd=${1:-"usage"}

  trap ctrl_c SIGINT
  # Detect and react to the user hitting CTRL + C

  case "${cmd}" in

    defaults)
      write_defaults
      ;;
    dotfiles)
      install_dotfiles
      ;;
    scripts)
      install_scripts
      ;;
    sudotouch)
      enable_touchid_sudo
      ;;
    secretive)
      install_secretive
      ;;
    sublimetext)
      install_sublime_text
      ;;
    tower)
      install_tower
      ;;
    deckset)
      install_deckset
      ;;
    maccy)
      install_maccy
      ;;
    texpad)
      install_texpad
      install_basictex
      install_latex_packages
      ;;
    basictex)
      install_basictex
      install_latex_packages
      ;;
    latex)
      install_latex_packages
      ;;
    blockblock)
      install_blockblock
      ;;
    oversight)
      install_oversight
      ;;
    brew)
      install_brew
      ;;
    brewfile)
      install_brewfile
      zsh_compinit_fix
      ;;
    compinit)
      zsh_compinit_fix
      ;;
    shell)
      change_shell
      ;;
    mdmprofile)
      open_system_preferences_mdm_profile
      ;;
    test)
      run_test
      ;;
    hailmary)
      # Execute all the functions
      # Order matters!
      echo -e "[üç∫] ${RED}Hailmary${RESET} engaged"
      sleep 3

      write_defaults
      install_dotfiles
      install_scripts
      enable_touchid_sudo
      install_secretive
      install_sublime_text
      install_tower
      install_deckset
      install_maccy
      install_texpad
      install_basictex
      install_latex_packages
      install_blockblock
      install_oversight
      install_brew
      install_brewfile
      zsh_compinit_fix
      open_system_preferences_mdm_profile
      ;;
    *)
      usage
      ;;
  esac

}

main "$@"
